
TITLE:
    PISA 2018 financial literacy SG-MSEM fixed-par approach Group 124

DATA:
    file = "C:\Users\tctan\implist.dat";

    type = imputation;

VARIABLE:
    names =
        FKI CNTRYID CNTSCHID CNTSTUID W_STU
        MATH READ FLIT                      ! PISA achievement variables
        MALE IMMI1GEN IMMI2GEN ESCS         ! Demographic info
        FCFMLRTY FLCONFIN                   ! Affect
        FLSCHOOL                            !   Lat var "Academic"
        NOBULLY                             !   Lat var "Safety"
        FLFAMILY                            !   Lat var "Community"
        W_SCH STRATIO                       ! School character
        EDUSHORT                            !   Lat var "inst. env."
        ;

    useobs = CNTRYID eq 124;          ! Pull out one country at a time

    usevar =
        FLIT                                ! Financial literacy outcome
        FLSCHOOL                            !   "Academic"
        NOBULLY                             !   "Safety"
        FLFAMILY                            !   "Community" (Financial socialisation)
        EDUSHORT STRATIO                    !   "Institutional environment"
        FCFMLRTY FLCONFIN                   ! L1 mediators
        MALE IMMI1GEN IMMI2GEN ESCS         ! L1 control vars
        ;

    ! Vars that ONLY appear in L1
    within =
        FCFMLRTY FLCONFIN                   ! Affective vars
        MALE IMMI1GEN IMMI2GEN ESCS         ! L1 control vars
        ;

    ! Vars that ONLY appear in L2
    between =
        EDUSHORT STRATIO                    ! L2: school
        ;

    missing = all (-99);

    weight = W_STU;                         ! Student weight
    wtscale = cluster;                      ! Scale L1 weight to cluster size
    bweight = W_SCH;                        ! School weight
    bwtscale = sample;                      ! Scale L2 weight to sample

    cluster = CNTSCHID;                     ! Cluster by school ID


ANALYSIS:
    processors = 8;

    type = twolevel;


MODEL:

%Within%                                    ! === L1: Student-level ===

    ! Indirect pathways (1st half): school climate vars onto mediators
        ! Onto mediator FAMILIARITY
    FCFMLRTY on FLSCHOOL;
    FCFMLRTY on NOBULLY;
    FCFMLRTY on FLFAMILY;
        ! Onto mediator CONFIDENCE
    FLCONFIN on FLSCHOOL;
    FLCONFIN on NOBULLY;
    FLCONFIN on FLFAMILY;

    ! Total effect
        ! Indirect pathways (2nd half): affective vars onto financial literacy
    FLIT on FCFMLRTY;
    FLIT on FLCONFIN;
        ! Direct pathways: school climate vars onto financial literacy
    FLIT on FLSCHOOL;
    FLIT on NOBULLY;
    FLIT on FLFAMILY;
        ! Control vars (demographic info)
    FLIT on MALE IMMI1GEN IMMI2GEN ESCS;

    ! Covariances
        ! Between school climate vars
    FLSCHOOL with NOBULLY;
    FLSCHOOL with FLFAMILY;
    NOBULLY with FLFAMILY;
        ! Between mediators
    FCFMLRTY with FLCONFIN;

%Between%                                   ! === L2: School-level ===

    ! School climate vars
    FLIT on
        FLSCHOOL NOBULLY FLFAMILY EDUSHORT
        STRATIO
        ;

    ! Covariances
    FLSCHOOL with NOBULLY;
    NOBULLY with FLFAMILY;
    FLFAMILY with EDUSHORT;

    FLSCHOOL with FLFAMILY;
    NOBULLY with EDUSHORT;

    FLSCHOOL with EDUSHORT;


MODEL INDIRECT:

    FLIT ind FLSCHOOL;
    FLIT ind NOBULLY;
    FLIT ind FLFAMILY;


OUTPUT:
    stdyx;                                  ! Fully standardised solution
    tech1;                                  ! Overview of model parameters
    tech3;                                  ! Parameter covariance structure
    svalues;                                ! Starting values
