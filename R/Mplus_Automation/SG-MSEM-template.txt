[[init]]
iterators = group;
group = 36 40 56 76 100 152 158 170 191 196 203 208 233 246 250 268
348 352 376 380 392 398 410 428 440 470 484 528 554 578 620 643 682
702 703 704 705 710 724 752 784 792 840 926 9134 9642 32001 156001;
groupnames#group = “AUS” “AUT” “BEL” “BRA” “BGR” “CHL” “TWN” “COL” 
“HRV” “CYP” “CZE” “DNK” “EST” “FIN” “FRA” “GEO” “HUN” “ISL” “ISR” 
“ITA” “JPN” “KAZ” “KOR” “LVA” “LTU” “MLT” “MEX” “NLD” “NZL” “NOR” 
“PRT” “RUS” “SAU” “SGP” “SVK” “VNM” “SVN” “ZAF” “ESP” “SWE” “ARE” 
“TUR” “USA” “ENG” “CAN” “ROU” “ARG” “CHN”;
filename = "SG-MSEM-[[group]].inp";
outputDirectory = "/Users/ronnysc/Documents/RESEARCH/TALIS2018/Research-SB-TN-RS/Re-Analyses/MSEM/SG-MSEM-ForEachCountry/Updated";
[[/init]]

TITLE:	TALIS 2018 SG-MSEM Fixed-Parameter Approach Group [[group]]
DATA:	FILE IS TALIS2018-Full.dat;
VARIABLE: 
NAMES ARE 
		IDCNTRY IDTEACH IDSCHOOL USCHOOLID UTEACHID IDLANG
		SCHWGT TCHWGT
		T1 T2 T3 T5 
		T6A1 T6B1 T6C1 T6D1 T6E1 T6F1 T6G1 T6H1 T6I1 T6J1 T6K1 T6L1
		T6A2 T6B2 T6C2 T6D2 T6E2 T6F2 T6G2 T6H2 T6I2 T6J2 T6K2 T6L2
		T7a T7b T7c T7d T7e T7f T7g T8 T11a T11b T11c T11d 
		T32a T32b T32c T32d
		T33a T33b T33c T33d T33e T33f T33g T33h
		T34a T34b T34c T34d T34e T34f T34g T34h T34i T34j T34k T34l 
		T34m T35a T35b T35c T35d T35e T35f T35g T35h
		T41a T41c T41d T41b
		T42a T42b T42c T42d T42e T42f T42g T42h T42i T42j T42k
		T42l T42m T42n T42o T42p T44 
		T45a T45b T45c T45d T45e T46
		T47a T47b T47c T47d T48a T48b T48c T48d T48e T48f T48g T48h
		T53a T53b T53e T53g T53i T53j T53c T53d T53f
		STBEH CLAIN CLASM COGAC COLES EFFPD EXCH PDBAR DISC
		PERUT PDIV PDPED VALP SATAT JSENV JSPRO SECLS SEINS SEENG
		SEFE SOCUT STAKE TEAM STUD WELS BLOAD TPRA COOP JOBSA
		SELF DIVP INNOP
		GII2018 GDP2018 logGDP PDI IDV MAS UAI LTO IVR
		T2cc T11bcc T2sc T11bsc T2ccsq T11bccsq T2scsq T11bscsq
		ckpck ccsit genped mixed;
	
USEVARIABLES ARE 
		T32a T32b T32c T32d
		T33a T33b T33c T33h
		T33d T33e T33f T33g
		T42e T42f T42g T42h
		T53c T53e T53g T53j
		INNOP
		T1 T2cc T2ccsq T3 ccsit mixed;

WITHIN = T1 T2cc T2ccsq T3 ccsit mixed;
! Teacher-level variables without school-level variation

MISSING ARE ALL (-99);
! All missing are coded as -99
 	
WEIGHT IS TCHWGT;
! Teacher weight (L1)
	
WTSCALE IS cluster;
! Scale the L1 weight to the cluster size
	
BWEIGHT = SCHWGT;
! School weight (L2)
	
BWTSCALE = sample;
! Scale the L2 weight to the sample

CLUSTER IS USCHOOLID;
! Clustering of the teacher data in schools
! Unique school ID

USEOBSERVATIONS IS IDCNTRY EQ [[group]];
! Country selection: [[groupnames#group]]


ANALYSIS:
	TYPE = TWOLEVEL;
	ESTIMATOR = MLR;
	PROCESSORS = 4(STARTS);  
	H1ITERATIONS = 100000;


MODEL:
%WITHIN%
	! MEASUREMENT MODEL
	! Team innovativeness
	TEAMw BY
		T32a@1.000
		T32b@1.046
		T32c@1.055
		T32d@0.979;

	! Collaboration
	COLESw BY
		T33a@1.000
		T33b@0.861
		T33c@0.948
		T33h@0.732;

	! Exchange
	EXCHw BY
		T33d@1.000 
		T33e@1.050
		T33f@1.227
		T33g@0.667;

	! Cognitive activation
	COGNw BY
		T42e@1.000
		T42f@1.014
		T42g@0.911
		T42h@1.065;

	! Job satisfaction
	JOBENVw BY
		T53c@1.000 
		T53e@1.207
		T53g@1.215
		T53j@0.732;

	! STRUCTURAL MODEL
	! Direct L1 effects of team innovativeness
	EXCHw ON TEAMw(ew);
	COLESw ON TEAMw(cw);
	JOBENVw ON TEAMw(jw);
	COGNw ON TEAMw(aw);
	INNOP ON TEAMw(iw);

	! Control variables
	TEAMw ON T1 T2cc T2ccsq T3;
	EXCHw ON T1 T2cc T2ccsq T3 ccsit mixed;
	COLESw ON T1 T2cc T2ccsq T3 ccsit mixed;
	JOBENVw ON T1 T2cc T2ccsq T3 ccsit mixed;
	COGNw ON T1 T2cc T2ccsq T3 ccsit mixed;
	INNOP ON T1 T2cc T2ccsq T3 ccsit mixed;

    ! Direct predictors
    TEAMw ON ccsit(ct);
    TEAMw ON mixed(mt);

	! Variances of the predictors
	ccsit(vccsit);
	mixed(vmixed);
	T1;
	T2cc;
	T2ccsq;
	T3;

	! Covariances among predictors
	T1 WITH T2cc T2ccsq T3 mixed ccsit;
	T2cc WITH T2ccsq T3 mixed ccsit;
	T2ccsq WITH T3 mixed ccsit;
	T3 WITH mixed ccsit;
	mixed WITH ccsit;

	! Covariances among residuals
	EXCHw WITH COLESw JOBENVw COGNw INNOP;
	COLESw WITH JOBENVw COGNw INNOP;
	JOBENVw WITH COGNw INNOP;
	COGNw WITH INNOP;

	! Variance of the mediator
	TEAMw(PsiW);

	! Variances of the outcome variables
	EXCHw(Tew);
	COLESw(Tcw);
	JOBENVw(Tjw);
	COGNw(Taw);
	INNOP(Tiw);


%BETWEEN%
	! MEASUREMENT MODEL
	! Team innovativeness
	TEAMb BY
		T32a@1.000
		T32b@1.046
		T32c@1.055
		T32d@0.979;

	! Collaboration
	COLESb BY
		T33a@1.000
		T33b@0.861
		T33c@0.948
		T33h@0.732;

	! Exchange
	EXCHb BY
		T33d@1.000 
		T33e@1.050
		T33f@1.227
		T33g@0.667;

	! Cognitive activation
	COGNb BY
		T42e@1.000
		T42f@1.014
		T42g@0.911
		T42h@1.065;

	! Job satisfaction
	JOBENVb BY
		T53c@1.000 
		T53e@1.207
		T53g@1.215
		T53j@0.732;

	! STRUCTURAL MODEL
	! Direct L2 effects of team innovativeness
	EXCHb ON TEAMb(eb);
	COLESb ON TEAMb(cb);
	JOBENVb ON TEAMb(jb);
	COGNb ON TEAMb(ab);
	INNOP ON TEAMb(ib);

	! Covariances among residuals
	EXCHb WITH COLESb JOBENVb COGNb INNOP;
	COLESb WITH JOBENVb COGNb INNOP;
	JOBENVb WITH COGNb INNOP;
	COGNb WITH INNOP;

	! Variance of the predictor
	TEAMb(PsiB);

	! Variances of the outcome variables
	EXCHb(Teb);
	COLESb(Tcb);
	JOBENVb(Tjb);
	COGNb(Tab);
	INNOP(Tib);

	! Item residual variances with reasonable starting values
	T32a-T53j*0.1;


MODEL CONSTRAINT:
NEW(te tc tj ta ti
Bte Btc Btj Bta Bti
inct_ew inct_cw inct_jw inct_aw inct_iw
inmt_ew inmt_cw inmt_jw inmt_aw inmt_iw
v_ct_ew v_ct_cw v_ct_jw v_ct_aw v_ct_iw
v_mt_ew v_mt_cw v_mt_jw v_mt_aw v_mt_iw);

! Contextual effects (estimated as the effect size ES2)
te = (eb-ew)*2*sqrt(PsiB)/sqrt(PsiW*ew**2+Tew);
tc = (cb-cw)*2*sqrt(PsiB)/sqrt(PsiW*cw**2+Tcw);
tj = (jb-jw)*2*sqrt(PsiB)/sqrt(PsiW*jw**2+Tjw);
ta = (ab-aw)*2*sqrt(PsiB)/sqrt(PsiW*aw**2+Taw);
ti = (ib-iw)*2*sqrt(PsiB)/sqrt(PsiW*iw**2+Tiw);

! Unstandardized contextual effects
Bte = (eb-ew);
Btc = (cb-cw);
Btj = (jb-jw);
Bta = (ab-aw);
Bti = (ib-iw);

! Unstandardized indirect effects
inct_ew = ct*ew;
inct_cw = ct*cw;
inct_jw = ct*jw;
inct_aw = ct*aw;
inct_iw = ct*iw;
inmt_ew = mt*ew;
inmt_cw = mt*cw;
inmt_jw = mt*jw;
inmt_aw = mt*aw;
inmt_iw = mt*iw;

! Standardized indirect effect sizes v (Lachowicz et al., 2018)
v_ct_ew = (inct_ew**2)*(vccsit/Tew);
v_ct_cw = (inct_cw**2)*(vccsit/Tcw);
v_ct_jw = (inct_jw**2)*(vccsit/Tjw);
v_ct_aw = (inct_aw**2)*(vccsit/Taw);
v_ct_iw = (inct_iw**2)*(vccsit/Tiw);
v_mt_ew = (inmt_ew**2)*(vmixed/Tew);
v_mt_cw = (inmt_cw**2)*(vmixed/Tcw);
v_mt_jw = (inmt_jw**2)*(vmixed/Tjw);
v_mt_aw = (inmt_aw**2)*(vmixed/Taw);
v_mt_iw = (inmt_iw**2)*(vmixed/Tiw);


MODEL INDIRECT:
! Indirect, direct, and total effects (available in STDYX)
EXCHw IND ccsit;
EXCHw IND mixed;
COLESw IND ccsit;
COLESw IND mixed;
JOBENVw IND ccsit;
JOBENVw IND mixed;
COGNw IND ccsit;
COGNw IND mixed;
INNOP IND ccsit;
INNOP IND mixed;


OUTPUT:
	STDYX;	! Fully standardized solution
	TECH1;	! Overview of model parameters
	TECH3;	! Parameter covariance structure
	SVALUES; ! Starting values