TITLE:
    M4: Financial literacy 3-level


DATA:
    file = "C:\Users\Admin\implist.dat";

    type = imputation;


VARIABLE:
    names =
        FKI CNTRYID CNTSCHID CNTSTUID W_STU
        MATH READ FLIT              ! PISA achievement variables
        MALE IMMI1GEN IMMI2GEN ESCS ! Demographic info
        FCFMLRTY FLCONFIN           ! Affect
        PERFEED TEACHINT FLSCHOOL   ! Lat var "Academic"
        BELONG NOBULLY              ! Lat var "Safety"
        FLFAMILY                    ! Lat var "Community"
        W_SCH STRATIO               ! School character
        EDUSHTG STAFFSHT            ! School resource shortage
        ;
    
    usev =
        FLIT                        ! Regressant
        FLSCHOOL                    !   "Academic"
        NOBULLY                     !   "Safety"
        FLFAMILY                    !   "Community" (Financial socialisation)
        EDUSHTG STRATIO             !   "Institutional environment"
        FCFMLRTY FLCONFIN           ! L1 mediators
        MALE IMMI1GEN IMMI2GEN ESCS ! L1 control vars
        FKI                         ! L3 predictor
        ;

    ! L3 label = country; L2 label = school
    cluster = CNTRYID CNTSCHID;

    ! Vars that ONLY appear in L1
    within =
        FCFMLRTY FLCONFIN
        MALE IMMI1GEN IMMI2GEN ESCS
        ;
    ! Vars that ONLY appear in L2 (label=CNTSCHID) and L3 (label=CNTRYID)
    between = (CNTSCHID) EDUSHTG STRATIO (CNTRYID) FKI;

    missing = all (-99);

    ! Weights (weight=student weight; b2weight=school weight)
    weight = W_STU;
    b2weight = W_SCH;


ANALYSIS:
    process = 16;

    type = threelevel;


MODEL:

%Within% ! L1: Student-level

    ! Indirect pathways: school climate vars onto mediators
        ! Onto mediator FAMILIARITY
    FCFMLRTY on FLSCHOOL NOBULLY FLFAMILY;
        ! Onto mediator CONFIDENCE
    FLCONFIN on FLSCHOOL NOBULLY FLFAMILY;

    ! Covariances between school climate vars
    FLSCHOOL with NOBULLY;
    FLSCHOOL with FLFAMILY;
    NOBULLY with FLFAMILY;
    ! Covariance between mediators
    FCFMLRTY with FLCONFIN;

    ! Total effect
    FLIT on
        FLSCHOOL NOBULLY FLFAMILY  ! Input variables
        FCFMLRTY FLCONFIN               ! Mediators
        MALE IMMI1GEN IMMI2GEN ESCS     ! Control: demographic
        ;

%Between CNTSCHID% ! L2: School-level (Structured model)

    FLIT on
        FLSCHOOL NOBULLY FLFAMILY EDUSHTG    ! School climate variables
        STRATIO                             ! Control: Student-teacher ratio
        ;

    FLSCHOOL with NOBULLY;
    NOBULLY with FLFAMILY;
    FLFAMILY with EDUSHTG;

    FLSCHOOL with FLFAMILY;
    NOBULLY with EDUSHTG;

    FLSCHOOL with EDUSHTG;

%Between CNTRYID%
    FLIT on
        FKI
        ;

OUTPUT:
    sampstat stdyx;
