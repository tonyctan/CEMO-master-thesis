[[init]]
iterators = group;
group =
    76 100 124 152 233
    246 268 360 380 428
    440 528 604 616 620
    643 688 703 724 840
;
groupnames#group =
    "BRA" "BGR" "CAN" "CHL" "EST"
    "FIN" "GEO" "IND" "ITA" "LVA"
    "LTU" "NLD" "PER" "POL" "PRT"
    "RUS" "SRB" "SVK" "ESP" "USA"
;
filename = "One-level_[[group]].inp";
outputDirectory = "M:\pc\Dokumenter\MSc\Thesis\Mplus\M1";
[[/init]]

TITLE:
    One-level [[group]]

DATA:
    file = "C:\Users\tctan\implist.dat";

    type = imputation;

VARIABLE:
    names =
        FKI CNTRYID CNTSCHID W_STU
        MATH READ FLIT                      ! PISA achievement variables
        MALE IMMI1GEN IMMI2GEN ESCS         ! Demographic info
        FCFMLRTY FLCONFIN                   ! Affect
        FLSCHOOL                            !   Lat var "Academic"
        NOBULLY                             !   Lat var "Safety"
        FLFAMILY                            !   Lat var "Community"
        W_SCH STRATIO                       ! School character
        EDUSHORT                            !   Lat var "inst. env."
        ;

    useobs = CNTRYID eq [[group]];          ! Pull out one country at a time

    usevar =
        FLIT                                ! PISA achievement variables
        FLSCHOOL                            !   Lat var "Academic"
        NOBULLY                             !   Lat var "Safety"
        FLFAMILY                            !   Lat var "Community"
        FCFMLRTY FLCONFIN                   ! Affect
        MALE IMMI1GEN IMMI2GEN ESCS         ! Demographic info
        ;

    weight = W_STU;                         ! Student weight


ANALYSIS:
    processors = 4;

    estimator = MLR;


MODEL:

    ! Indirect pathways (1st half): school climate vars onto mediators
        ! Onto mediator FAMILIARITY
    FCFMLRTY on FLSCHOOL NOBULLY FLFAMILY;
        ! Onto mediator CONFIDENCE
    FLCONFIN on FLSCHOOL NOBULLY FLFAMILY;

    ! Total effect
    FLIT on
        ! Direct pathways: school climate vars onto financial literacy
        FLSCHOOL NOBULLY FLFAMILY
        ! Indirect pathways (2nd half): affective vars onto financial literacy
        FCFMLRTY FLCONFIN
        ! Control vars (demographic info)
        MALE IMMI1GEN IMMI2GEN ESCS
        ;

    ! Covariances
        ! Between school climate vars
    FLSCHOOL with NOBULLY;
    FLSCHOOL with FLFAMILY;
    NOBULLY with FLFAMILY;
        ! Between mediators
    FCFMLRTY with FLCONFIN;
        ! SES with FLFAMILY
    ESCS with FLFAMILY;

MODEL INDIRECT:

! Indirect effects
    FLIT ind FLSCHOOL;
    FLIT ind NOBULLY;
    FLIT ind FLFAMILY;


OUTPUT:
    stdyx                                   ! Fully standardised solution
    mod(all)                                ! Mod indices (all)
    ;
