
TITLE:
    One-level 100

DATA:
    file = "~/implist.dat";

    type = imputation;

VARIABLE:
    names =
        FKI CNTRYID CNTSCHID W_STU
        MATH READ FLIT                      ! PISA achievement variables
        MALE IMMI1GEN IMMI2GEN ESCS         ! Demographic info
        FCFMLRTY FLCONFIN                   ! Affect
        FLSCHOOL                            !   Lat var "Academic"
        NOBULLY                             !   Lat var "Safety"
        FLFAMILY                            !   Lat var "Community"
        W_SCH STRATIO                       ! School character
        EDUSHORT                            !   Lat var "inst. env."
        ;

    useobs = CNTRYID eq 100;          ! Pull out one country at a time

    usevar =
        FLIT                                ! PISA achievement variables
        FLSCHOOL                            !   Lat var "Academic"
        NOBULLY                             !   Lat var "Safety"
        FLFAMILY                            !   Lat var "Community"
        FCFMLRTY FLCONFIN                   ! Affect
        MALE IMMI1GEN IMMI2GEN ESCS         ! Demographic info
        ;

    weight = W_STU;                         ! Student weight


ANALYSIS:
    processors = 8;

    estimator = MLR;


MODEL:

    ! Indirect pathways (1st half): school climate vars onto mediators
        ! Onto mediator FAMILIARITY
    FCFMLRTY on FLSCHOOL FLFAMILY NOBULLY ESCS IMMI2GEN MALE;
        ! Onto mediator CONFIDENCE
    FLCONFIN on FLSCHOOL FLFAMILY NOBULLY ESCS IMMI2GEN MALE;

    ! Total effect
    FLIT on
        ! Indirect pathways (2nd half): affective vars onto financial literacy
        FCFMLRTY FLCONFIN
        ! Direct pathways: school climate vars onto financial literacy
        FLSCHOOL FLFAMILY NOBULLY
        ! Dmographic vars
        ESCS IMMI1GEN
        ;

    ! Covariances
        ! Between school climate vars
    FLSCHOOL with FLFAMILY;
    FLFAMILY with NOBULLY;
    FLSCHOOL with NOBULLY;
        ! Between mediators
    FCFMLRTY with FLCONFIN;

        ! SES with school climate vars
    ESCS with FLSCHOOL FLFAMILY;
        ! SES with demographic vars
    ESCS with IMMI1GEN IMMI2GEN;

MODEL INDIRECT:
! Indirect effects
    FLIT ind FLSCHOOL;
    FLIT ind FLFAMILY;
    FLIT ind NOBULLY;

    FLIT ind ESCS;
    FLIT ind IMMI2GEN;
    FLIT ind MALE;


OUTPUT:
    stdyx                                   ! Fully standardised solution
    ;
