TITLE:
    Financial literacy single-level only


DATA:
    file = "C:\Users\Tony\implist.dat";

    type = imputation;


VARIABLE:
    names =
        FKI CNTRYID CNTSCHID CNTSTUID W_STU
        MATH READ FLIT              ! Financial literacy
        MALE IMMI1GEN IMMI2GEN ESCS ! Control variables
        FCFMLRTY FLCONFIN           ! Mediators
        PERFEED TEACHINT FLSCHOOL   ! Lat var "Academic"
        BELONG BULLY                ! Lat var "Safety"
        FLFAMILY                    ! Lat var "Community"
        W_SCH PRIVATE STRATIO       ! School character
        EDUST STAFFST               ! School resource shortage
        ;

    usev =
        FLIT                        ! Regressant
        PERFEED TEACHINT FLSCHOOL   !   Lat var "Academic"
        BELONG BULLY                !   Lat var "Safety"
        FLFAMILY                    !   Financial socialisation
        FCFMLRTY FLCONFIN           !   Mediators
        MALE IMMI1GEN IMMI2GEN ESCS !   Control: demographic
        ;

    missing = all (-99);

    weight = W_STU;


ANALYSIS:
    process = 8;

    estimator = MLR;

MODEL:

    ! Lat var ACADEMIC
    ACADEMIC by PERFEED TEACHINT FLSCHOOL;
    ! Lat var SAFTY
    SAFETY by BELONG BULLY;

    ! Covariances
    ACADEMIC with SAFETY;
    ACADEMIC with FLFAMILY;
    SAFETY with FLFAMILY;
    FCFMLRTY with FLCONFIN;

    ! Total effect
    FLIT on
        ACADEMIC SAFETY FLFAMILY        ! Input variables
        FCFMLRTY FLCONFIN               ! Mediators
        MALE IMMI1GEN IMMI2GEN ESCS     ! Control: demographic
        ;

    ! Onto mediator FAMILIARITY
    FCFMLRTY on ACADEMIC SAFETY FLFAMILY;

    ! Onto mediator CONFIDENCE
    FLCONFIN on ACADEMIC SAFETY FLFAMILY;

MODEL INDIRECT:

    ! Indirect effects
    FLIT ind ACADEMIC;
    FLIT ind SAFETY;
    FLIT ind FLFAMILY;


OUTPUT:
    sampstat stdyx;
