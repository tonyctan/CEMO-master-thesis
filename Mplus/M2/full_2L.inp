TITLE:
    Financial literacy 2-level: L1 full model


DATA:
    file = "M:/pc/Dokumenter/MSc/Thesis/Data/Mplus/implist.dat";

    type = imputation;


VARIABLE:
    names =
        W_CNT FKI CNTRYID CNTSCHID CNTSTUID BOOKID W_STU 
        MATH READ FLIT              ! Numeracy, literacy, finlit
        MALE IMMI1GEN IMMI2GEN ESCS ! Control variables
        FCFMLRTY FLCONFIN           ! Mediators
        PERFEED TEACHINT FLSCHOOL   ! Lat var "Academic"
        DISCRIM BELONG BULLY        ! Lat var "Safety"
        FLFAMILY                    ! Lat var "Community"
        W_SCH PRIVATE STRATIO       ! School character
        EDUST STAFFST               ! School resource shortage
        ;

    usev =
        CNTSCHID                    ! Level label
        FLIT                        ! Regressant
        PERFEED TEACHINT FLSCHOOL   !   Lat var "Academic"
        DISCRIM BELONG BULLY        !   Lat var "Safety"
        FLFAMILY                    !   Financial socialisation
        FCFMLRTY FLCONFIN           !   Mediators
        MALE IMMI1GEN IMMI2GEN ESCS !   Control: demographic
        MATH READ                   !   Control: numeracy and literacy
        ;

    cluster = CNTSCHID;

!	between = (CNTRYID) FKI;

    within =
!        PERFEED TEACHINT FLSCHOOL
!        DISCRIM BELONG BULLY
!        FLFAMILY
        FCFMLRTY FLCONFIN
        MALE IMMI1GEN IMMI2GEN ESCS
        MATH READ
        ;

    missing = all (-99);

    weight = W_STU;

!    bweight = W_SCH;


ANALYSIS:
    process = 8;

    type = twolevel;


MODEL:

%Between%
    FLIT;

%Within%

    ! Lat var ACADEMIC
    ACADEMIC_w by PERFEED TEACHINT FLSCHOOL;
    ! Lat var SAFTY
    SAFETY_w by DISCRIM BELONG BULLY;
	! Decompose Financial socialisation to within and between
	FLFAMILY_w by FLFAMILY@1; 

    ! Total effect
    FLIT on
        ACADEMIC_w SAFETY_w FLFAMILY_w  ! Input variables
        FCFMLRTY FLCONFIN               ! Mediators
        MALE IMMI1GEN IMMI2GEN ESCS     ! Control: demographic
        MATH READ                       ! Control: numeracy and literacy
        ;

    ! Onto mediator FAMILIARITY
    FCFMLRTY on ACADEMIC_w SAFETY_w FLFAMILY_w;

    ! Onto mediator CONFIDENCE
    FLCONFIN on ACADEMIC_w SAFETY_w FLFAMILY_w;

MODEL INDIRECT:

    ! Indirect effects
    FLIT ind ACADEMIC_w;
    FLIT ind SAFETY_w;
    FLIT ind FLFAMILY_w;


OUTPUT:
    sampstat tech1; !tech8;
