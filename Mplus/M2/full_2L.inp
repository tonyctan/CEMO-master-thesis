TITLE:
    Financial literacy 2-level: L1 full model


DATA:
    file = "C:\Users\Tony\implist.dat";

    type = imputation;


VARIABLE:
    names =
        FKI CNTRYID CNTSCHID CNTSTUID W_STU
        MATH READ FLIT              ! Financial literacy
        MALE IMMI1GEN IMMI2GEN ESCS ! Control variables
        FCFMLRTY FLCONFIN           ! Mediators
        PERFEED TEACHINT FLSCHOOL   ! Lat var "Academic"
        BELONG BULLY                ! Lat var "Safety"
        FLFAMILY                    ! Lat var "Community"
        W_SCH PRIVATE STRATIO       ! School character
        EDUST STAFFST               ! School resource shortage
        ;

    usev =
!       CNTSCHID                    ! Level label (DO NOT include cluster labels in usev)
        FLIT                        ! Regressant
        PERFEED TEACHINT FLSCHOOL   !   Lat var "Academic"
        BELONG BULLY                !   Lat var "Safety"
        FLFAMILY                    !   Financial socialisation
        FCFMLRTY FLCONFIN           !   Mediators
        MALE IMMI1GEN IMMI2GEN ESCS !   Control: demographic
!        MATH READ                   !   Control: numeracy and literacy
        ;
    
    ! Nested by schools
    cluster = CNTSCHID;

    ! Only list vars that are used EXCLUSIVELY on L1
    within =
!        PERFEED TEACHINT FLSCHOOL
!        BELONG BULLY
!        FLFAMILY
!        FCFMLRTY FLCONFIN
        MALE IMMI1GEN IMMI2GEN ESCS
!        MATH READ
        ;

    missing = all (-99);

    ! Student weights
    weight = W_STU;
    ! Schoo weights
    bweight = W_SCH;


ANALYSIS:
    process = 8;

    type = twolevel;


MODEL:

%Between% ! L2: School-level (Saturated model)
    ! Estimate the variances of the following variables
    PERFEED TEACHINT FLSCHOOL
    BELONG BULLY
    FLFAMILY
    FCFMLRTY FLCONFIN
    FLIT;

    ! Estimate the covariances between all the var above
    PERFEED with TEACHINT FLSCHOOL
        BELONG BULLY
        FLFAMILY
        FCFMLRTY FLCONFIN
        FLIT;

    TEACHINT with FLSCHOOL
        BELONG BULLY
        FLFAMILY
        FCFMLRTY FLCONFIN
        FLIT;

    FLSCHOOL with
        BELONG BULLY
        FLFAMILY
        FCFMLRTY FLCONFIN
        FLIT;

    BELONG with BULLY
        FLFAMILY
        FCFMLRTY FLCONFIN
        FLIT;

    BULLY with
        FLFAMILY
        FCFMLRTY FLCONFIN
        FLIT;

    FLFAMILY with
        FCFMLRTY FLCONFIN
        FLIT;

    FCFMLRTY with FLCONFIN
        FLIT;

    FLCONFIN with
        FLIT;

%Within% ! L1: Student-level

    ! Lat var ACADEMIC
    ACADEMIC_w by PERFEED TEACHINT FLSCHOOL;
    ! Lat var SAFTY
    SAFETY_w by BELONG BULLY;

	! Decompose Financial socialisation to within and between
	FLFAMILY_w by FLFAMILY@1;
    ! Estimate the within variance of FLFAMILY
    FLFAMILY;

    ! Covariances between school climate vars
    ACADEMIC_w with SAFETY_w;
    ACADEMIC_w with FLFAMILY_w@0;   ! Turn off cov between ACADEMIC and FLFAMILY
    SAFETY_w with FLFAMILY_w@0;     ! Turn off cov between SAFETY and FLFAMILY
    ! Covariance between mediators
    FCFMLRTY with FLCONFIN;

    ! Indirect pathways: school climate vars onto mediators
        ! Onto mediator FAMILIARITY
    FCFMLRTY on ACADEMIC_w SAFETY_w FLFAMILY_w;
        ! Onto mediator CONFIDENCE
    FLCONFIN on ACADEMIC_w SAFETY_w FLFAMILY_w;

    ! Total effect
    FLIT on
        ACADEMIC_w SAFETY_w FLFAMILY_w  ! Input variables
        FCFMLRTY FLCONFIN               ! Mediators
        MALE IMMI1GEN IMMI2GEN ESCS     ! Control: demographic
!       MATH READ                       ! Control: numeracy and literacy
        ;


MODEL INDIRECT:

    ! Indirect effects
    FLIT ind ACADEMIC_w;
    FLIT ind SAFETY_w;
    FLIT ind FLFAMILY_w;


OUTPUT:
    sampstat stdyx;
