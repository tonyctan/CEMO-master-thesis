TITLE:
    M2: Financial literacy 2-level saturated model


DATA:
    file = "C:\Users\tctan\implist.dat";

    type = imputation;


VARIABLE:
    names =
        FKI CNTRYID CNTSCHID CNTSTUID W_STU
        MATH READ FLIT              ! Financial literacy
        MALE IMMI1GEN IMMI2GEN ESCS ! Control variables
        FCFMLRTY FLCONFIN           ! Mediators
        PERFEED TEACHINT FLSCHOOL   ! Lat var "Academic"
        BELONG NOBULLY              ! Lat var "Safety"
        FLFAMILY                    ! Lat var "Community"
        W_SCH STRATIO               ! School character
        EDUSHTG STAFFSHT             ! School resource shortage
        ;

    usev =
        FLIT                        ! Regressant
        FLSCHOOL                    !   "Academic"
        NOBULLY                     !   "Safety"
        FLFAMILY                    !   "Community" (Financial socialisation)
        EDUSHTG STRATIO               !   "Institutional environment"
        FCFMLRTY FLCONFIN           !   Mediators
        MALE IMMI1GEN IMMI2GEN ESCS !   Control: demographic
               
        ;
    
    ! Nested by schools
    cluster = CNTSCHID;

    ! Only list vars that are used EXCLUSIVELY on L1
    within =
        FCFMLRTY FLCONFIN
        MALE IMMI1GEN IMMI2GEN ESCS
        ;
        
    between = 
        EDUSHTG STRATIO
        ;

    missing = all (-99);

    ! Student weights
    weight = W_STU;
    ! Schoo weights
    bweight = W_SCH;


ANALYSIS:
    type = twolevel;

    estimator = MLR;

    processors = 8;



MODEL:

%Within% ! L1: Student-level

    ! Indirect pathways: school climate vars onto mediators
        ! Onto mediator FAMILIARITY
    FCFMLRTY on FLSCHOOL NOBULLY FLFAMILY;
        ! Onto mediator CONFIDENCE
    FLCONFIN on FLSCHOOL NOBULLY FLFAMILY;

    ! Covariances between school climate vars
    FLSCHOOL with NOBULLY;
    FLSCHOOL with FLFAMILY;
    NOBULLY with FLFAMILY;
    ! Covariance between mediators
    FCFMLRTY with FLCONFIN;

    ! Total effect
    FLIT on
        FLSCHOOL NOBULLY FLFAMILY  ! Input variables
        FCFMLRTY FLCONFIN               ! Mediators
        MALE IMMI1GEN IMMI2GEN ESCS     ! Control: demographic
        ;

%Between% ! L2: School-level (Saturated model)

    FLIT-STRATIO WITH FLIT-STRATIO;


MODEL INDIRECT:

    ! Indirect effects
    FLIT ind FLSCHOOL;
    FLIT ind NOBULLY;
    FLIT ind FLFAMILY;

OUTPUT:
    sampstat stdyx;
