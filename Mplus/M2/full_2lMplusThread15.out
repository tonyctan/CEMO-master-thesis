Mplus VERSION 8.3
MUTHEN & MUTHEN
12/07/2020  11:51 AM

INPUT INSTRUCTIONS

  TITLE:
      M2: Financial literacy 2-level: L1 no numceracy or literacy


  DATA:
      file = "C:\Users\Admin\implist.dat";

      type = imputation;


  VARIABLE:
      names =
          FKI CNTRYID CNTSCHID CNTSTUID W_STU
          MATH READ FLIT              ! Financial literacy
          MALE IMMI1GEN IMMI2GEN ESCS ! Control variables
          FCFMLRTY FLCONFIN           ! Mediators
          PERFEED TEACHINT FLSCHOOL   ! Lat var "Academic"
          BELONG NOBULLY              ! Lat var "Safety"
          FLFAMILY                    ! Lat var "Community"
          W_SCH STRATIO               ! School character
          EDUSHT STAFFSHT             ! School resource shortage
          ;

      usev =
          FLIT                        ! Regressant
          PERFEED TEACHINT FLSCHOOL   !   Lat var "Academic"
          BELONG NOBULLY              !   Lat var "Safety"
          FLFAMILY                    !   Financial socialisation
          FCFMLRTY FLCONFIN           !   Mediators
          MALE IMMI1GEN IMMI2GEN ESCS !   Control: demographic
          ;

      ! Nested by schools
      cluster = CNTSCHID;

      ! Only list vars that are used EXCLUSIVELY on L1
      within =
  !        PERFEED TEACHINT FLSCHOOL
  !        BELONG NOBULLY
  !        FLFAMILY
  !        FCFMLRTY FLCONFIN
          MALE IMMI1GEN IMMI2GEN ESCS
          ;

      missing = all (-99);

      ! Student weights
      weight = W_STU;
      ! Schoo weights
      bweight = W_SCH;


  ANALYSIS:
      type = twolevel;

      estimator = MLR;

      processors = 16;

      starts = 16;


  MODEL:

  %Between% ! L2: School-level (Saturated model)

      ! Estimate the variances of the following variables
      PERFEED TEACHINT FLSCHOOL
      BELONG NOBULLY
      FLFAMILY
  !    FCFMLRTY FLCONFIN
      FLIT;

      ! Estimate the covariances between all the var above
      PERFEED with TEACHINT FLSCHOOL
          BELONG NOBULLY
          FLFAMILY
  !        FCFMLRTY FLCONFIN
          FLIT;

      TEACHINT with FLSCHOOL
          BELONG NOBULLY
          FLFAMILY
  !        FCFMLRTY FLCONFIN
          FLIT;

      FLSCHOOL with
          BELONG NOBULLY
          FLFAMILY
  !        FCFMLRTY FLCONFIN
          FLIT;

      BELONG with NOBULLY
          FLFAMILY
  !        FCFMLRTY FLCONFIN
          FLIT;

      NOBULLY with
          FLFAMILY
  !        FCFMLRTY FLCONFIN
          FLIT;

      FLFAMILY with
  !        FCFMLRTY FLCONFIN
          FLIT;

  !    FCFMLRTY with FLCONFIN
  !        FLIT;

  !    FLCONFIN with
  !        FLIT;

  %Within% ! L1: Student-level

      ! Lat var ACADEMIC
      ACADEMIC_w by PERFEED TEACHINT FLSCHOOL;
      ! Lat var SAFTY
      SAFETY_w by BELONG NOBULLY;

  	! Decompose Financial socialisation to within and between
  	FLFAMILY_w by FLFAMILY@1;
      ! Estimate the within variance of FLFAMILY
      FLFAMILY;

      ! Covariances between school climate vars
      ACADEMIC_w with SAFETY_w;
      ACADEMIC_w with FLFAMILY_w@0;   ! Turn off cov between ACADEMIC and FLFAMILY
      SAFETY_w with FLFAMILY_w@0;     ! Turn off cov between SAFETY and FLFAMILY
      ! Covariance between mediators
      FCFMLRTY with FLCONFIN;

      ! Indirect pathways: school climate vars onto mediators
          ! Onto mediator FAMILIARITY
      FCFMLRTY on ACADEMIC_w SAFETY_w FLFAMILY_w;
          ! Onto mediator CONFIDENCE
      FLCONFIN on ACADEMIC_w SAFETY_w FLFAMILY_w;

      ! Total effect
      FLIT on
          ACADEMIC_w SAFETY_w FLFAMILY_w  ! Input variables
          FCFMLRTY FLCONFIN               ! Mediators
          MALE IMMI1GEN IMMI2GEN ESCS     ! Control: demographic
          ;


  !MODEL INDIRECT:

      ! Indirect effects
  !    FLIT ind ACADEMIC_w;
  !    FLIT ind SAFETY_w;
  !    FLIT ind FLFAMILY_w;


  OUTPUT:
      sampstat stdyx;



*** WARNING in MODEL command
  A y-variable has been declared on the within level but not referred to on
  the between level.  Please check that this is what is intended.  If this is not intended,
  specify the variable as a within variable.  Problem with:  FCFMLRTY
*** WARNING in MODEL command
  A y-variable has been declared on the within level but not referred to on
  the between level.  Please check that this is what is intended.  If this is not intended,
  specify the variable as a within variable.  Problem with:  FLCONFIN
   2 WARNING(S) FOUND IN THE INPUT INSTRUCTIONS




M2: Financial literacy 2-level: L1 no numceracy or literacy

