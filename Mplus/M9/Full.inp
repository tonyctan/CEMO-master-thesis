TITLE:
    Financial literacy 3-level: Full model


DATA:
    file = "M:/pc/Dokumenter/MSc/Thesis/Data/Mplus/implist.dat";

    type = imputation;


VARIABLE:
    names =
        W_CNT FKI CNTRYID CNTSCHID CNTSTUID BOOKID W_STU
        MATH READ FLIT              ! Achievement var
        MALE IMMI1GEN IMMI2GEN ESCS ! Control var
        FCFMLRTY FLCONFIN           ! Mediators
        PERFEED TEACHINT FLSCHOOL   ! Lat var "Academic"
        DISCRIM BELONG BULLY        ! Lat var "Safety"
        FLFAMILY                    ! Lat var "Community" [Now in manifest var]
        W_SCH PRIVATE STRATIO EDUST STAFFST    ! School var
        ;

    usev =
        CNTRYID CNTSCHID            ! Level labels
        FLIT                        ! Regressant
        FKI                         ! L3 regressor
        EDUST STAFFST STRATIO       ! L2 regressors
                                    ! L1 regressors:
        PERFEED TEACHINT FLSCHOOL   !   Lat var "Academic"
        DISCRIM BELONG BULLY        !   Lat var "Safety"
        FLFAMILY                    !   Financial socialisation
        FCFMLRTY FLCONFIN           !   Mediators
        MALE IMMI1GEN IMMI2GEN ESCS !   Control: demographic
        MATH READ                   !   Control: numeracy and literacy
        ;

    cluster = CNTRYID CNTSCHID;

    within =                        ! L1 var names
        FCFMLRTY FLCONFIN
        MALE IMMI1GEN IMMI2GEN ESCS
        MATH READ
        ;

    between =
        (CNTSCHID) EDUST STAFFST STRATIO    ! Var unique to L2
        (CNTRYID) FKI                       ! L3 var
        ;

    missing = all (-99);

    weight = W_STU;

    b2weight = W_SCH;


ANALYSIS:
    process = 8;

    type = threelevel;

!    estimator = Bayes;         ![estimator = Bayes is not available for data type = imputation]
!    biterations = (30000);


MODEL:

%within%
    ! Lat var ACADEMIC
    ACADEMIC_w by PERFEED TEACHINT FLSCHOOL;
    ! Lat var SAFTY
    SAFETY_w by DISCRIM BELONG BULLY;
    ! Decompose Financial socialisation to within and between parts
    FLFAMILY_w by FLFAMILY@1;

    ! Total effect
    FLIT on
        ACADEMIC_w SAFETY_w FLFAMILY_w  ! Input variables
        FCFMLRTY FLCONFIN               ! Mediators
        MALE IMMI1GEN IMMI2GEN ESCS     ! Control: demographic
        MATH READ                       ! Control: numeracy and literacy
        ;

    ! School climate var onto mediator FAMILIARITY
    FCFMLRTY on ACADEMIC_w SAFETY_w FLFAMILY_w;

    ! School climate var onto mediator CONFIDENCE
    FLCONFIN on ACADEMIC_w SAFETY_w FLFAMILY_w;

%between CNTSCHID%
    ! Lat var ACADEMIC
    ACADEMIC_b by PERFEED TEACHINT FLSCHOOL;
    ! Lat var SAFTY
    SAFETY_b by DISCRIM BELONG BULLY;
    ! Decompose Financial socialisation to within and between parts
    FLFAMILY_b by FLFAMILY@1;
    ! Lat var RESOURCE
    RESOURCE by EDUST STAFFST;

    FLIT on
        ACADEMIC_b SAFETY_b FLFAMILY_b RESOURCE ! School climate var
        STRATIO                                 ! Control var
        ;

    ACADEMIC_b with SAFETY_b;
    ACADEMIC_b with FLFAMILY_b;
    ACADEMIC_b with RESOURCE;
    SAFETY_b with FLFAMILY_b;
    FLFAMILY_b with RESOURCE;

%between CNTRYID%
    FLIT on FKI;

    FKI;

!MODEL INDIRECT:            [MODEL INDIRECT is not available for analysis type = threelevel]
!
!    ! Indirect effects
!    FLIT ind ACADEMIC;
!    FLIT ind SAFETY;
!    FLIT ind FLFAMILY;


OUTPUT:
    sampstat tech1 tech8;
