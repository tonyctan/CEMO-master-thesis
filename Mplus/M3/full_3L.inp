TITLE:
    M3: Financial literacy 3-level L1 full model with on numeracy or literacy


DATA:
    file = "C:\Users\Tony\implist.dat";

    type = imputation;


VARIABLE:
    names =
        FKI CNTRYID CNTSCHID CNTSTUID W_STU
        MATH READ FLIT              ! Financial literacy
        MALE IMMI1GEN IMMI2GEN ESCS ! Control variables
        FCFMLRTY FLCONFIN           ! Mediators
        PERFEED TEACHINT FLSCHOOL   ! Lat var "Academic"
        BELONG BULLY                ! Lat var "Safety"
        FLFAMILY                    ! Lat var "Community"
        W_SCH PRIVATE STRATIO       ! School character
        EDUST STAFFST               ! School resource shortage
        ;
    
    usev =
        CNTRYID CNTSCHID            ! Level labels
        FLIT                        ! Regressant
        PERFEED TEACHINT FLSCHOOL   !   Lat var "Academic"
        BELONG BULLY                !   Lat var "Safety"
        FLFAMILY                    !   Financial socialisation
        FCFMLRTY FLCONFIN           !   Mediators
        MALE IMMI1GEN IMMI2GEN ESCS !   Control: demographic
        MATH READ                   !   Control: numeracy and literacy
        ;

    cluster = CNTRYID CNTSCHID;

    within =
!        PERFEED TEACHINT FLSCHOOL
!        DISCRIM BELONG BULLY
!        FLFAMILY
        FCFMLRTY FLCONFIN
        MALE IMMI1GEN IMMI2GEN ESCS
        MATH READ
        ;

!    missing = all (-99);

    weight = W_STU;

    b2weight = W_SCH;


ANALYSIS:
    process = 8;

    type = threelevel;


MODEL:
%Between CNTRYID%
    FLIT;

%Between CNTSCHID%
    FLIT;

%Within%

    ! Lat var ACADEMIC
    ACADEMIC_w by PERFEED TEACHINT FLSCHOOL;
    ! Lat var SAFTY
    SAFETY_w by BELONG BULLY;
    ! Decompose Financial socialisation to within and between
    FLFAMILY_w by FLFAMILY;

    ! Covariances
    ACADEMIC_w with SAFETY_w;
    ACADEMIC_w with FLFAMILY_w;
    SAFETY_w with FLFAMILY_w;
    FCFMLRTY with FLCONFIN;

    ! Total effect
    FLIT on
        ACADEMIC_w SAFETY_w FLFAMILY_w  ! Input variables
        FCFMLRTY FLCONFIN               ! Mediators
        MALE IMMI1GEN IMMI2GEN ESCS     ! Control: demographic
        MATH READ                       ! Control: numeracy and literacy
        ;

    ! Onto mediator FAMILIARITY
    FCFMLRTY on ACADEMIC_w SAFETY_w FLFAMILY_w;

    ! Onto mediator CONFIDENCE
    FLCONFIN on ACADEMIC_w SAFETY_w FLFAMILY_w;


OUTPUT:
    sampstat tech1;
